apply plugin: 'signing'
apply plugin: 'maven-publish'

ext {
  publishProfile = findProperty('publish.profile')
}

signing {
  required {
    publishProperty("sign").toString() == 'true'
  }
  sign publishing.publications
}

artifacts {
  archives jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveClassifier = 'javadoc'
  from javadoc.destinationDir
}

task install(dependsOn: publishToMavenLocal) {
  group = 'Publishing'
  description = 'Installs artifacts to local Maven repository'
}

publishing {
  repositories {
    maven {
      credentials {
        username publishProperty("username")
        password publishProperty("password")
      }
      url = version.endsWith('-SNAPSHOT')
          ? publishProperty("snapshotsUrl")
          : publishProperty("releasesUrl")
    }
  }
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      afterEvaluate {
        pom {
          name = project.ext.moduleName
          description = project.ext.moduleDescription
          url = 'https://github.com/febit/wit'
          organization {
            name = 'Febit'
            url = 'https://github.com/febit'
          }
          licenses {
            license {
              name = 'The New BSD License'
              url = 'https://github.com/febit/wit/blob/master/LICENSE'
              distribution = 'repo'
            }
          }
          scm {
            url = 'https://github.com/febit/wit'
            connection = 'scm:git:https://github.com/febit/wit.git'
            developerConnection = 'scm:git:https://github.com/febit/wit.git'
          }
          issueManagement {
            system = 'GitHub'
            url = 'https://github.com/febit/wit/issues'
          }
          developers {
            developer {
              id = 'zqq'
              name = 'Zhu Qingqing'
              email = 'zqq@febit.org'
              timezone = '+8'
            }
          }
        }
      }
    }
  }
}

def publishProperty(def key) {
  if (publishProfile == null) {
    return null
  }
  return findProperty("publish." + publishProfile + "." + key)
}
