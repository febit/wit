plugins {
  id 'org.febit.standard-java' version '1.1.0'
  id 'org.febit.standard-maven-publish' version '1.1.0'
}

group 'org.febit.wit'
version '3.0.0-SNAPSHOT'

ext {
  v = [
      febitCommons  : '3.0.0-SNAPSHOT',
      commonsLogging: '1.1',
      servletApi    : '2.5'
  ]
}

configure(subprojects.findAll {
  !it.name.endsWith('-bom')
}) {
  apply plugin: 'java-library'
  apply plugin: 'jacoco'

  group rootProject.group
  version rootProject.version

  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
  }

  test {
    useJUnitPlatform()
  }

  repositories {
    maven { url "https://repo.huaweicloud.com/repository/maven" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
  }

  dependencyManagement {
    imports {
      mavenBom "org.febit:febit-commons-dependencies:${v.febitCommons}"
    }
    dependencies {
      dependency "commons-logging:commons-logging-api:${v.commonsLogging}"
      dependency "javax.servlet:servlet-api:${v.servletApi}"
    }
  }

  dependencies {
    compileOnly 'org.febit:febit-annotations-optional'
    testCompileOnly 'org.febit:febit-annotations-optional'

    testImplementation 'org.febit:febit-commons-test'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.slf4j:slf4j-simple'
  }
}
