// Copyright (c) 2013-2016, febit.org. All Rights Reserved.
package org.febit.wit.core;

import org.febit.wit.exceptions.ParseException;
import org.febit.wit.lang.ast.*;
import org.febit.wit.lang.ast.expr.*;
import org.febit.wit.lang.ast.oper.*;
import org.febit.wit.lang.ast.stat.*;
import org.febit.wit.lang.ALU;
import org.febit.wit.lang.AstFactory;
import org.febit.wit.lang.AstUtils;
import org.febit.wit.util.ClassNameBand;
import org.febit.wit.util.Stack;

import static org.febit.wit.lang.ast.expr.FunctionDeclarePart.ArgumentInfo;

/* Terminals */
terminal Integer    VAR,
                    IF, ELSE,
                    FOR,
                    THIS, SUPER,
                    SWITCH, CASE, DEFAULT,
                    DO, WHILE,
                    THROW, TRY, CATCH, FINALLY,
                    NEW,
                    INSTANCEOF,
                    FUNCTION,
                    ECHO,
                    STATIC, NATIVE,
                    IMPORT, INCLUDE, NATIVE_IMPORT,
                    BREAK, CONTINUE, RETURN,

                    PLUSPLUS, MINUSMINUS,  // ++ ==
                    PLUS, MINUS, MULT, DIV, MOD,  // + - * /
                    LSHIFT, RSHIFT, URSHIFT,  // << >> >>>
                    LT, GT, LTEQ, GTEQ, EQEQ, NOTEQ,  // < > <= >= == !=
                    AND, XOR, OR, COMP,  // & ^ | ~
                    ANDAND, OROR, NOT,  // && || !
                    QUESTION,// ?
                    SELFEQ,  // (+ - * / %)= (<< >> >>>)= (& ^ |)=
                    UMINUS,
                    DOT,  // .
                    COLON, COLONCOLON,  // : ::
                    COMMA,  // ,
                    SEMICOLON,  // ;
                    LBRACE, RBRACE, INTERPOLATION_END, // { }
                    LPAREN, RPAREN,  // ( )
                    LBRACK, RBRACK,  // [ ]
                    LDEBUG, RDEBUG, LRDEBUG,  // [? ?] [?]
                    EQGT,  // =>
                    RPAREN_MINUSGT, MINUSGT, // )-> ->
                    DYNAMIC_DOT, // @ .~
                    DOTDOT,  // ..
                    EQ,   // =
                    TEMPLATE_STRING_START, TEMPLATE_STRING_INTERPOLATION_END;


terminal String     TEMPLATE_STRING_INTERPOLATION_START, TEMPLATE_STRING_END, IDENTIFIER, METHOD_REFERENCE; // Ident, MethodReference
terminal char[]     TEXT_STATEMENT;
terminal Object     DIRECT_VALUE;
terminal Integer    CONST;

/* nonterminal */
nonterminal ClassNameBand classPureName, className;
nonterminal Class javaClass;
nonterminal TemplateAST templateAST;

/* Statement */
nonterminal Statement statement, ifStat, varPart;
nonterminal IBlock block, caseBlockStat;
nonterminal Expression varAssign;
nonterminal ImportPart importPart1, importPart2;
nonterminal SwitchPart switchPart, switchPart0;
nonterminal AbstractForInPart forInHead, forInPart, lambdaForHead, lambdaForHead2;
nonterminal WhilePart whilePart;
nonterminal TryPart tryPart, catchPart, finallyPart;

/* Expression */
nonterminal Expression expression, expression_statementable;
nonterminal Expression[] mapPropertyDef;
nonterminal MapValue mapValue;
nonterminal TemplateStringExpressionPart templateStringExprPart;
nonterminal Integer superCount;
nonterminal Expression contextValueExpr;

/* function & Lambda */
nonterminal ArgumentInfo funcArg;
nonterminal Expression funcExecuteExpr;
nonterminal FunctionDeclarePart funcHead, funcStatementHead, mapPropertyFuncHead;
nonterminal Expression lambdaExpr;
nonterminal FunctionDeclarePart lambdaExprHead;
nonterminal FunctionDeclarePart lambdaForHead1, lambdaForMapHead1;

/* precedence */
precedence left     SEMICOLON; // ;
precedence left     COMMA; // ,
precedence left     LDEBUG; // [?
precedence left     RBRACK; // ]
precedence left     COLON;  // :
precedence right    SELFEQ, EQ; // (+ - * / %)=  (<< >> >>>)=  (& ^ |)=  =
precedence left     DOTDOT;  // ..
precedence right    QUESTION; // ?
precedence left     OROR; // ||
precedence left     ANDAND; // &&
precedence left     XOR; // ^
precedence left     OR; // |
precedence left     AND; // &
precedence left     EQEQ, NOTEQ; // == !=
precedence left     LT, LTEQ, GT, GTEQ; // < <= > >=  //Unsupport: instanceOf
precedence left     LSHIFT, RSHIFT, URSHIFT; // << >> >>>
precedence left     PLUS, MINUS; // + -
precedence left     MULT, DIV, MOD; // * / %
precedence right    PLUSPLUS, MINUSMINUS, UMINUS, NOT, COMP; // ++ -- - ! ~
precedence left     EQGT;  // =>
precedence right    RPAREN_MINUSGT, MINUSGT; // ->
precedence left     DOT, LBRACK, LBRACE, LPAREN, RPAREN, DYNAMIC_DOT; // . [ { ( ) @


templateAST     ::= statement[]:list ?
                    {: return createTemplateAST(%list%); :}
                    ;


classPureName   ::= IDENTIFIER:ident
                    {: return new ClassNameBand(%ident%); :}
                    |classPureName:part DOT IDENTIFIER:ident
                    {: return (%part%).append(%ident%); :}
                    ;

className       ::= classPureName:part
                    {: return %part%; :}
                    |className:part LBRACK RBRACK
                    {: return (%part%).plusArrayDepth(); :}
                    ;


javaClass       ::= className:nameBand
                    {: return toClass(%nameBand%, %nameBand.symbol%.pos); :}
                    ;


/* statements  */
statement       ::= expression_statementable:$ SEMICOLON
                    |SEMICOLON
                    {: return NoopStatement.INSTANCE; :}

                    |LRDEBUG:sym
                    {: return createBreakpointStatement(null, null, %sym.symbol%.pos); :}
                    |LDEBUG:sym expression:label COLON RDEBUG
                    {: return createBreakpointStatement(%label%, null, %sym.symbol%.pos); :}

                    |expression:expr INTERPOLATION_END
                    {: return createInterpolation(%expr%); :}

                    |block:$

                    |ifStat:$

                    //switch
                    |switchPart:part
                    {: return (%part%).pop(0); :}
                    |IDENTIFIER:label COLON switchPart:part
                    {: return (%part%).pop(getLabelIndex(%label%)); :}

                    //while
                    |whilePart:part
                    {: return (%part%).pop(0); :}
                    |IDENTIFIER:label COLON whilePart:part
                    {: return (%part%).pop(getLabelIndex(%label%)); :}

                    //forIn
                    |forInPart:part
                    {: return (%part%).pop(0); :}
                    |IDENTIFIER:label COLON forInPart:part
                    {: return (%part%).pop(getLabelIndex(%label%)); :}

                    //RedirectOut
                    |block:stat EQGT:sym expression:expr SEMICOLON
                    {: return new RedirectOut(%stat%, castToAssignableExpression(%expr%), %sym.symbol%.pos); :}

                    //textStat
                    |TEXT_STATEMENT:sym
                    {: return createTextStatement(%sym%, %sym.symbol%.pos); :}

                    //
                    |CONST IDENTIFIER:ident EQ expression:expr SEMICOLON
                    {: assignConst(%ident%, %expr%, %ident.symbol%.pos); return NoopStatement.INSTANCE; :}

                    //var
                    |VAR varPart[COMMA]:parts SEMICOLON
                    {: return createStatementGroup(%parts%, %parts.symbol%.pos); :}

                    //echoStat
                    |ECHO:sym expression:expr SEMICOLON
                    {: return new Echo(%expr%, %sym.symbol%.pos); :}

                    //@import
                    |NATIVE_IMPORT:sym classPureName:nameBand SEMICOLON
                    {: registerClass(%nameBand%, %sym.symbol%.pos); return NoopStatement.INSTANCE; :}

                    //BREAK
                    |BREAK:sym IDENTIFIER:label? SEMICOLON
                    {: return new Break(getLabelIndex(%label%), %sym.symbol%.pos); :}

                    //CONTINUE
                    |CONTINUE:sym IDENTIFIER:label? SEMICOLON
                    {: return new Continue(getLabelIndex(%label%), %sym.symbol%.pos); :}

                    //RETURN
                    |RETURN:sym expression:expr? SEMICOLON
                    {: return new Return(%expr%, %sym.symbol%.pos); :}

                    //importStat
                    |importPart1:part SEMICOLON
                    {: return (%part%).pop(this.template); :}
                    |importPart2:part SEMICOLON
                    {: return (%part%).pop(this.template); :}

                    //includeStat
                    |INCLUDE:sym expression:expr mapValue:paramsExpr? SEMICOLON
                    {: return new Include(%expr%, %paramsExpr%, this.template.getName(), %sym.symbol%.pos); :}

                    //funcStatement
                    |funcStatementHead:part LBRACE statement[]:list? RBRACE
                    {: return (%part%).pop(%list%); :}

                    //
                    |(catchPart:$ | finallyPart:$):part
                    {: return (%part%).pop(); :}

                    //
                    | THROW:sym expression:expr SEMICOLON
                    {: return new Throw(%expr%, %sym.symbol%.pos); :}
                    ;


varAssign       ::= IDENTIFIER:ident EQ:sym (expression:$ | varAssign:$):expr
                    {: return createAssign(declareVarAndCreateContextValue(%ident%, %ident.symbol%.pos), %expr%, %sym.symbol%.pos); :}
                    ;

varPart         ::= varAssign:$
                    | IDENTIFIER:ident
                    {: return declareVar(%ident%, %ident.symbol%.pos); :}
                    | LBRACK IDENTIFIER[COMMA]:idents RBRACK EQ:sym expression:expr
                    {: return createGroupAssign(declareVarAndCreateContextValues(%idents%, %idents.symbol%.pos), %expr%, %sym.symbol%.pos); :}
                    ;


importPart1     ::= IMPORT:sym expression:expr mapValue:paramsExpr?
                    {: return new ImportPart(%expr%, %paramsExpr%, %sym.symbol%.pos); :}
                    ;


importPart2     ::= importPart1:part IDENTIFIER:ident
                    {: return (%part%).append(%ident%, createContextValue(0, %ident%, %ident.symbol%.pos)); :}
                    |importPart1:part contextValueExpr:valueExpr EQ IDENTIFIER:ident
                    {: return (%part%).append(%ident%, %valueExpr%); :}
                    |importPart2:part COMMA IDENTIFIER:ident
                    {: return (%part%).append(%ident%, createContextValue(0, %ident%, %ident.symbol%.pos)); :}
                    |importPart2:part COMMA contextValueExpr:valueExpr EQ IDENTIFIER:ident
                    {: return (%part%).append(%ident%, %valueExpr%); :}
                    ;


block           ::= (LBRACE {: varmgr.push(); return null; :}) statement[]:list? RBRACE
                    {: return createIBlock(%list%, varmgr.pop(), %list.symbol%.pos); :}
                    ;


tryPart         ::= (TRY LBRACE {: varmgr.push(); return null; :}) statement[]:list? RBRACE
                    {: return createTryPart(%list%, varmgr.pop(), %list.symbol%.pos); :}
                    ;

catchPart       ::= tryPart:part (CATCH LPAREN IDENTIFIER:ident RPAREN LBRACE {: varmgr.push(); return varmgr.assignVariant(%ident%, %ident.symbol%.pos); :}):varIndex statement[]:list? RBRACE
                    {: return (%part%).setCatchStat((Integer)(%varIndex%), createIBlock(%list%, varmgr.pop(), %list.symbol%.pos)); :}
                    ;

finallyPart     ::= (tryPart:$ | catchPart:$):part (FINALLY LBRACE {: varmgr.push(); return null; :}) statement[]:list? RBRACE
                    {: return (%part%).setFinalStat(createIBlock(%list%, varmgr.pop(), %list.symbol%.pos)); :}
                    ;


ifStat          ::= IF:sym LPAREN expression:expr RPAREN block:body (ELSE block:$ | ELSE ifStat:$):elseBody?
                    {: return createIfStatement(%expr%, %body%, (Statement) (%elseBody%), %sym.symbol%.pos); :}
                    ;

forInHead       ::= FOR:sym LPAREN IDENTIFIER:item COLON expression:items RPAREN
                    {: return new ForInPart(%item%, %items%, this.varmgr, %sym.symbol%.pos); :}
                    |FOR:sym LPAREN IDENTIFIER:key COMMA IDENTIFIER:value COLON expression:map RPAREN
                    {: return new ForMapPart(%key%, %value%, %map%, this.varmgr, %sym.symbol%.pos); :}
                    ;


lambdaForHead1  ::= FOR LPAREN IDENTIFIER:ident MINUSGT:sym
                    {: return new FunctionDeclarePart(this.varmgr, %sym.symbol%.pos).appendArg(%ident%); :}
                    ;


lambdaForMapHead1   ::= FOR LPAREN IDENTIFIER:key COMMA IDENTIFIER:value MINUSGT:sym
                    {: return new FunctionDeclarePart(this.varmgr, %sym.symbol%.pos).appendArg(%key%).appendArg(%value%); :}
                    ;


lambdaForHead2  ::= lambdaForHead1:part expression:expr COLON
                    {: return new ForInPart((%part%).getArg(0), (%part%).popFunctionDeclare(%expr%), this.varmgr, %part.symbol%.pos); :}
                    |lambdaForHead1:part LBRACE statement[]:list RBRACE COLON
                    {: return new ForInPart((%part%).getArg(0), (%part%).popFunctionDeclare(%list%), this.varmgr, %part.symbol%.pos); :}
                    |lambdaForMapHead1:part expression:expr COLON
                    {: return new ForMapPart((%part%).getArg(0), (%part%).getArg(1), (%part%).popFunctionDeclare(%expr%), this.varmgr, %part.symbol%.pos); :}
                    |lambdaForMapHead1:part LBRACE statement[]:list RBRACE COLON
                    {: return new ForMapPart((%part%).getArg(0), (%part%).getArg(1), (%part%).popFunctionDeclare(%list%), this.varmgr, %part.symbol%.pos); :}
                    ;


lambdaForHead   ::= lambdaForHead2:part expression:items RPAREN
                    {: return (%part%).setCollectionExpr(%items%); :}
                    ;


forInPart       ::= (forInHead:$ | lambdaForHead:$):part LBRACE statement[]:list? RBRACE (ELSE block:$):elseBody?
                    {: return (%part%).setBodies(createIBlock(%list%, varmgr.pop(), %list.symbol%.pos), %elseBody%); :}
                    ;


whilePart       ::= WHILE:sym LPAREN expression:whileExpr RPAREN block:body
                    {: return new WhilePart(%whileExpr%, %body%, true, %sym.symbol%.pos); :}
                    |DO:sym block:body WHILE LPAREN expression:whileExpr RPAREN SEMICOLON
                    {: return new WhilePart(%whileExpr%, %body%, false, %sym.symbol%.pos); :}
                    ;


caseBlockStat   ::= {: varmgr.push(); return null; :} statement[]:list?
                    {: return createIBlock(%list%, varmgr.pop(), %list.symbol%.pos); :}
                    ;


switchPart0     ::=
                    {: return new SwitchPart(); :}
                    | CASE:sym MINUS:negative? DIRECT_VALUE:key COLON caseBlockStat:body switchPart0:part
                    {: return (%part%).appendCase((%negative% == null)?(%key%):ALU.negative(%key%), %body%, %sym.symbol%.pos); :}
                    | DEFAULT:sym COLON caseBlockStat:body switchPart0:part
                    {: return (%part%).appendCase(null, %body%, %sym.symbol%.pos); :}
                    ;


switchPart      ::= SWITCH:sym LPAREN expression:switchExpr RPAREN LBRACE TEXT_STATEMENT ? switchPart0:part RBRACE
                    {: return (%part%).setSwitchExpr(%switchExpr%, %sym.symbol%.pos); :}
                    ;



/* Expressions */

expression_statementable ::=
                    //assignOp =
                    expression:lexpr EQ:sym expression:expr
                    {: return createAssign(castToAssignableExpression(%lexpr%), %expr%, %sym.symbol%.pos); :}

                    |LBRACK expression[COMMA]:list COMMA? RBRACK EQ:sym expression:expr
                    {: return createGroupAssign(AstUtils.toExpressionArray(%list%), %expr%, %sym.symbol%.pos); :}

                    //plusPlusOp ++
                    |PLUSPLUS:sym expression:expr
                    {: return new PlusPlusBefore(castToAssignableExpression(%expr%), %sym.symbol%.pos); :}
                    |expression:expr PLUSPLUS:sym
                    {: return new PlusPlusAfter(castToAssignableExpression(%expr%), %sym.symbol%.pos); :}

                    //minusMinusOp --
                    |MINUSMINUS:sym expression:expr
                    {: return new MinusMinusBefore(castToAssignableExpression(%expr%), %sym.symbol%.pos); :}
                    |expression:expr MINUSMINUS:sym
                    {: return new MinusMinusAfter(castToAssignableExpression(%expr%), %sym.symbol%.pos); :}

                    //selfOp
                    |expression:lexpr SELFEQ:sym expression:rexpr
                    {: return createSelfOperator(%lexpr%, %sym%, %rexpr%, %sym.symbol%.pos); :}

                    //
                    |funcExecuteExpr:$
                    ;


expression      ::= expression_statementable:$

                    |LDEBUG:sym expression:expr RDEBUG
                    {: return createBreakpointExpression(null, %expr%, %sym.symbol%.pos); :}

                    |LDEBUG:sym expression:label COLON expression:expr RDEBUG
                    {: return createBreakpointExpression(%label%, %expr%, %sym.symbol%.pos); :}

                    //bitNotOp ~
                    |COMP:sym expression:expr
                    {: return createOperator(%expr%, %sym.symbol%); :}

                    //negativeOp  -
                    |MINUS:sym expression:expr %prec UMINUS
                    {: return createOperator(%expr%, %sym.symbol%); :}

                    //notOp !
                    |NOT:sym expression:expr
                    {: return createOperator(%expr%, %sym.symbol%); :}

                    //propertyOp .
                    |expression:expr DOT:sym IDENTIFIER:ident
                    {: return new PropertyOperator(%expr%, %ident%, %sym.symbol%.pos); :}

                    //indexOp []
                    |expression:lexpr LBRACK:sym expression:rexpr RBRACK
                    {: return new IndexOperator(%lexpr%, %rexpr%, %sym.symbol%.pos); :}

                    /***********  binaries *************/
                    //-- binary:  *
                    |expression:lexpr MULT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  /
                    |expression:lexpr DIV:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  %
                    |expression:lexpr MOD:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  +
                    |expression:lexpr PLUS:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  -
                    |expression:lexpr MINUS:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  <<
                    |expression:lexpr LSHIFT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  >>
                    |expression:lexpr RSHIFT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  >>>
                    |expression:lexpr URSHIFT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  <
                    |expression:lexpr LT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  <=
                    |expression:lexpr LTEQ:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  >
                    |expression:lexpr GT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  >=
                    |expression:lexpr GTEQ:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  ==
                    |expression:lexpr EQEQ:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  !=
                    |expression:lexpr NOTEQ:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  &
                    |expression:lexpr AND:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  |
                    |expression:lexpr OR:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  ^
                    |expression:lexpr XOR:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  &&
                    |expression:lexpr ANDAND:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  ..
                    |expression:lexpr DOTDOT:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}
                    //-- binary:  ||
                    |expression:lexpr OROR:sym expression:rexpr
                    {: return createBiOperator(%lexpr%, %sym.symbol%, %rexpr%); :}

                    // ? :
                    |expression:ifExpr QUESTION:sym expression:lexpr COLON expression:rexpr
                    {: return new IfOperator(%ifExpr%, %lexpr%, %rexpr%, %sym.symbol%.pos); :}

                    //
                    |DIRECT_VALUE:sym
                    {: return createDirectValue(%sym.symbol%); :}

                    // ()
                    |LPAREN expression:$ RPAREN

                    //funcDeclareExpr
                    |funcHead:part LBRACE statement[]:list? RBRACE
                    {: return (%part%).pop(%list%); :}

                    //arrayValue
                    |LBRACK:sym RBRACK
                    {: return AstFactory.emptyArrayValue(%sym.symbol%.pos); :}
                    |LBRACK:sym expression[COMMA]:list COMMA? RBRACK
                    {: return AstFactory.arrayValue(%list%, %sym.symbol%.pos); :}

                    //NativeNewArrayDeclareExpression
                    |NATIVE:sym LBRACK RBRACK javaClass:javaClass
                    {: return createNativeNewArrayDeclareExpression(%javaClass%, %sym.symbol%.pos); :}

                    //nativeMethodExpr
                    |NATIVE:sym classPureName:nameBand DOT IDENTIFIER:ident LPAREN javaClass[COMMA]:list? RPAREN
                    {: return createNativeMethodDeclareExpression(toClass(%nameBand%, %nameBand.symbol%.pos), %ident%, %list%, %sym.symbol%.pos); :}

                    //native static field
                    |NATIVE classPureName:nameBand
                    {: return createNativeStaticValue(%nameBand%, %nameBand.symbol%.pos); :}

                    //nativeConstructorExpr
                    |NATIVE:sym NEW classPureName:nameBand LPAREN javaClass[COMMA]:list? RPAREN
                    {: return createNativeConstructorDeclareExpression(toClass(%nameBand%, %nameBand.symbol%.pos), %list%, %sym.symbol%.pos); :}

                    |METHOD_REFERENCE:ref
                    {: return createMethodReference(%ref%, %ref.symbol%.pos); :}

                    |contextValueExpr:$
                    |mapValue:$

                    //Lambda
                    |lambdaExpr:$

                    // template string
                    |templateStringExprPart:part TEMPLATE_STRING_END:end
                    {: return (%part%).add(createDirectValue(%end.symbol%)).pop(); :}
                    ;

// super
superCount      ::= SUPER DOT
                    {: return 1; :}
                    |superCount:count SUPER DOT
                    {: return %count% + 1; :}
                    ;


// ident  super...ident  this.ident

contextValueExpr    ::= IDENTIFIER:ident
                        {: return createContextValue(0, %ident%, %ident.symbol%.pos); :}
                        |superCount:count IDENTIFIER:ident
                        {: return createContextValue(%count%, %ident%, %ident.symbol%.pos); :}
                        |FOR DOT IDENTIFIER:ident
                        {: return createContextValue(0, ("for." + %ident%), %ident.symbol%.pos); :}
                        |superCount:count FOR DOT IDENTIFIER:ident
                        {: return createContextValue(%count%, ("for." + %ident%), %ident.symbol%.pos); :}
                        ;

// map {key:expr, ...}

mapPropertyDef        ::= DIRECT_VALUE:key COLON expression:expr
                        {: return new Expression[]{createDirectValue(%key.symbol%), %expr%}; :}
                        |MINUS:sym DIRECT_VALUE:key COLON expression:expr
                        {: return new Expression[]{new DirectValue(ALU.negative(%key%), %key.symbol%.pos), %expr%}; :}
                        |IDENTIFIER:ident
                        {: return new Expression[]{createDirectValue(%ident.symbol%), createContextValue(0, %ident%, %ident.symbol%.pos)}; :}
                        |IDENTIFIER:ident COLON expression:expr
                        {: return new Expression[]{createDirectValue(%ident.symbol%), %expr%}; :}
                       // |IDENTIFIER:ident mapPropertyFuncHead:part LBRACE statement[]:list? RBRACE
                       // {: return new Expression[]{createDirectValue(%ident.symbol%), (%part%).pop(%list%)}; :}
                        |LBRACK expression:key RBRACK COLON expression:expr
                        {: return new Expression[]{%key%, %expr%}; :}
                        ;


mapValue            ::= LBRACE:sym RBRACE
                        {: return createMapValue(null, %sym.symbol%.pos); :}
                        |LBRACE:sym mapPropertyDef[COMMA]:list COMMA? RBRACE
                        {: return createMapValue(%list%, %sym.symbol%.pos); :}
                        ;


templateStringExprPart  ::= TEMPLATE_STRING_START:sym
                            {: return new TemplateStringExpressionPart(%sym.symbol%.pos); :}
                            | templateStringExprPart:part TEMPLATE_STRING_INTERPOLATION_START:start expression:expr TEMPLATE_STRING_INTERPOLATION_END
                            {: return (%part%).add(createDirectValue(%start.symbol%)).add(%expr%); :}
                            ;

/* function  */
funcArg             ::= IDENTIFIER:ident
                        {: return new ArgumentInfo(%ident%, null); :}
                        | IDENTIFIER:ident EQ expression:expr
                        {: return new ArgumentInfo(%ident%, AstUtils.calcConst(%expr%)); :}
                        ;


funcStatementHead   ::= FUNCTION:sym IDENTIFIER:ident LPAREN funcArg[COMMA]:args? RPAREN
                        {: return new FunctionDeclarePart(%ident%, this.varmgr, %ident.symbol%.pos).appendArgs(%args%); :}
                        ;

// mapPropertyFuncHead ::= LPAREN:sym funcArg[COMMA]:args? RPAREN
//                         {: return new FunctionDeclarePart(this.varmgr, %sym.symbol%.pos).appendArgs(%args%); :}
//                         ;

funcHead            ::= FUNCTION:sym LPAREN funcArg[COMMA]:args? RPAREN
                        {: return new FunctionDeclarePart(this.varmgr, %sym.symbol%.pos).appendArgs(%args%); :}
                        ;

funcExecuteExpr     ::= expression:funcExpr LPAREN RPAREN
                        {: return createMethodExecute(%funcExpr%, AstUtils.emptyExpressions(), %funcExpr.symbol%.pos); :}
                        |expression:funcExpr LPAREN expression[COMMA]:list COMMA? RPAREN
                        {: return createMethodExecute(%funcExpr%, AstUtils.toExpressionArray(%list%), %funcExpr.symbol%.pos); :}
                        |expression:param0 DYNAMIC_DOT IDENTIFIER:ident LPAREN RPAREN
                        {: return createDynamicNativeMethodExecute(%param0%, %ident%, AstUtils.emptyExpressions(), %ident.symbol%.pos); :}
                        |expression:param0 DYNAMIC_DOT IDENTIFIER:ident LPAREN expression[COMMA]:list COMMA? RPAREN
                        {: return createDynamicNativeMethodExecute(%param0%, %ident%, AstUtils.toExpressionArray(%list%), %ident.symbol%.pos); :}
                        ;


/* lambda */
lambdaExprHead      ::= IDENTIFIER:ident MINUSGT:sym
                        {: return new FunctionDeclarePart(this.varmgr, %sym.symbol%.pos).appendArg(%ident%); :}
                        |LPAREN funcArg[COMMA]:args? RPAREN_MINUSGT:sym
                        {: return new FunctionDeclarePart(this.varmgr, %sym.symbol%.pos).appendArgs(%args%); :}
                        ;


lambdaExpr          ::= lambdaExprHead:part expression:expr
                        {: return (%part%).pop(%expr%); :}
                        |lambdaExprHead:part LBRACE statement[]:list RBRACE
                        {: return (%part%).pop(%list%); :}
                        ;

